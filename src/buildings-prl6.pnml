// This is part of Polish Buildings Set NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

#define PRL6_FILE   "gfx/buildings-prl6.png"

/* Sprites and spritelayouts */
#define BUILDING_PRL6_SPRITES(Model, Xcoord, Flags) \
spriteset(sset_building_prl6_##Model##_ground, PRL6_FILE) { \
    tmpl_sprite_ground(Xcoord,  160, 1) \
} \
spriteset(sset_building_prl6_##Model, PRL6_FILE) { \
    tmpl_sprite_build80_flags(Xcoord,   20, 1, Flags) \
} \
spriteset(sset_building_prl6_##Model##_ground_snow, PRL6_FILE) { \
    tmpl_sprite_ground(Xcoord,  360, 1) \
} \
spriteset(sset_building_prl6_##Model##_snow, PRL6_FILE) { \
    tmpl_sprite_build80_flags(Xcoord,  220, 1, Flags) \
} \
spritelayout slay_building_prl6_##Model { \
    ground { \
        sprite: LOAD_TEMP(0); \
 	} \
    childsprite { \
	    sprite: sset_building_prl6_##Model##_ground; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_building_prl6_##Model; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 80; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
} \
spritelayout slay_building_prl6_##Model##_snow { \
    ground { \
        sprite: LOAD_TEMP(0); \
 	} \
    childsprite { \
	    sprite: sset_building_prl6_##Model##_ground_snow; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_building_prl6_##Model##_snow; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 80; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
}
/* Model #1 */
BUILDING_PRL6_SPRITES(1, 4, ANIM)

/* Switches */
switch (FEAT_HOUSES, SELF, switch_building_prl6_modern_normal_graphics, [
    STORE_TEMP(GROUNDSPRITE_CONCRETE, 0), construction_state]) {
    0: return switch_constr_stage_buldoger;
    1: return switch_constr_stage_narrow1r_1;
    2: return switch_constr_stage_narrow1r_2;
    return slay_building_prl6_1;
}
switch (FEAT_HOUSES, SELF, switch_building_prl6_modern_snow_graphics, [
    STORE_TEMP(GROUNDSPRITE_CONCRETE, 0), construction_state]) {
    0: return switch_constr_stage_buldoger;
    1: return switch_constr_stage_narrow1r_1;
    2: return switch_constr_stage_narrow1r_2;
    return slay_building_prl6_1_snow;
}
switch (FEAT_HOUSES, SELF, switch_building_prl6_modern_graphics, terrain_type) {
    TILETYPE_SNOW: switch_building_prl6_modern_snow_graphics;
    switch_building_prl6_modern_normal_graphics;
}

/* House definition */
item (FEAT_HOUSES, item_building_prl6_modern) {
    property {
        /* common properties */
        substitute:                 0x1C;
        name:                       string(STR_OFFICE_UNTYPICAL);
        override:                   0x1C;
        building_flags:             bitmask(HOUSE_FLAG_ANIMATE);
        population:                 120;
        mail_multiplier:            58;
        accepted_cargos:            [[PASS, 7], [MAIL, 2], [GOOD, 4]];
        local_authority_impact:     100;
        removal_cost_multiplier:    107;
        probability:                PLB_PROBABILITY;
        years_available:            [1971, 2007];
        minimum_lifetime:           LIFETIME_MAJOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_CENTRE), PLB_CLIMATES];
        random_colours:             [COLOUR_PINK, COLOUR_CREAM, COLOUR_BLUE, COLOUR_GREEN];
        refresh_multiplier:         0;
        animation_info:             [ANIMATION_NON_LOOPING, 12];
        animation_speed:            4;
        watched_cargo_types:        [];
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_building_prl6_modern_graphics;
//        cargo_type_accept:  switch_houses_cargo_type_accept;
        anim_control:       switch_anim_control_75;
    }
}