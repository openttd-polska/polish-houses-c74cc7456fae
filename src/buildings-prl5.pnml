// This is part of Polish Buildings Set NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Sprites and spritelayouts */
#define BUILDING_PRL5_SPRITES(Model, Xcoord, Flags) \
spriteset(sset_building_prl5_##Model##_ground, "gfx/buildings-prl5.png") { \
    tmpl_sprite_ground(Xcoord,  160, 1) \
} \
spriteset(sset_building_prl5_##Model, "gfx/buildings-prl5.png") { \
    tmpl_sprite_build80_flags(Xcoord,   20, 1, Flags) \
} \
spriteset(sset_building_prl5_##Model##_ground_snow, "gfx/buildings-prl5.png") { \
    tmpl_sprite_ground(Xcoord,  360, 1) \
} \
spriteset(sset_building_prl5_##Model##_snow, "gfx/buildings-prl5.png") { \
    tmpl_sprite_build80_flags(Xcoord,  220, 1, Flags) \
} \
spritelayout slay_building_prl5_##Model { \
    ground { \
        sprite: LOAD_TEMP(0); \
 	} \
    childsprite { \
	    sprite: sset_building_prl5_##Model##_ground; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_building_prl5_##Model; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 80; \
        recolour_mode: RECOLOUR_NONE; \
    } \
} \
spritelayout slay_building_prl5_##Model##_snow { \
    ground { \
        sprite: LOAD_TEMP(0); \
 	} \
    childsprite { \
	    sprite: sset_building_prl5_##Model##_ground_snow; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_building_prl5_##Model##_snow; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 80; \
        recolour_mode: RECOLOUR_NONE; \
    } \
}
/* Model #1 */
BUILDING_PRL5_SPRITES(1, 4, ANIM)
/* Model #2 */
BUILDING_PRL5_SPRITES(2, 80, ANIM)

/* Switches */
switch (FEAT_HOUSES, SELF, switch_building_prl5_untypical_normal_graphics, [
    STORE_TEMP(GROUNDSPRITE_CONCRETE, 0), construction_state]) {
    0: return switch_constr_stage_buldoger;
    1: return switch_constr_stage_narrow1r_1;
    2: return switch_constr_stage_narrow1r_2;
    return slay_building_prl5_1;
}
switch (FEAT_HOUSES, SELF, switch_building_prl5_untypical_snow_graphics, [
    STORE_TEMP(GROUNDSPRITE_CONCRETE, 0), construction_state]) {
    0: return switch_constr_stage_buldoger;
    1: return switch_constr_stage_narrow1r_1;
    2: return switch_constr_stage_narrow1r_2;
    return slay_building_prl5_1_snow;
}
switch (FEAT_HOUSES, SELF, switch_building_prl5_untypical_graphics, terrain_type) {
    TILETYPE_SNOW: switch_building_prl5_untypical_snow_graphics;
    switch_building_prl5_untypical_normal_graphics;
}

switch (FEAT_HOUSES, SELF, switch_building_prl5_office_normal_graphics, [
    STORE_TEMP(GROUNDSPRITE_NORMAL, 0), construction_state]) {
    0: return switch_constr_stage_buldoger;
    1: return switch_constr_stage_square_1;
    2: return switch_constr_stage_square_2;
    return slay_building_prl5_2;
}
switch (FEAT_HOUSES, SELF, switch_building_prl5_office_snow_graphics, [
    STORE_TEMP(GROUNDSPRITE_SNOW, 0), construction_state]) {
    0: return switch_constr_stage_buldoger;
    1: return switch_constr_stage_square_1;
    2: return switch_constr_stage_square_2;
    return slay_building_prl5_2_snow;
}
switch (FEAT_HOUSES, SELF, switch_building_prl5_office_graphics, terrain_type) {
    TILETYPE_SNOW: switch_building_prl5_office_snow_graphics;
    switch_building_prl5_office_normal_graphics;
}

/* House definition */
item (FEAT_HOUSES, item_building_prl5_untypical) {
    property {
        /* common properties */
        substitute:                 0x1C;
        name:                       string(STR_OFFICE_UNTYPICAL);
        override:                   0x1C;
        building_flags:             bitmask(HOUSE_FLAG_ANIMATE);
        population:                 120;
        mail_multiplier:            58;
        accepted_cargos:            [[PASS, 7], [MAIL, 2], [GOOD, 4]];
        local_authority_impact:     100;
        removal_cost_multiplier:    107;
        probability:                PLB_PROBABILITY;
        years_available:            [1967, 1995];
        minimum_lifetime:           LIFETIME_MAJOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_CENTRE, TOWNZONE_INNER_SUBURB), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        animation_info:             [ANIMATION_NON_LOOPING, 12];
        animation_speed:            4;
        watched_cargo_types:        [];
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_building_prl5_untypical_graphics;
//        cargo_type_accept:  switch_houses_cargo_type_accept;
        anim_control:       switch_anim_control_75;
    }
}

item (FEAT_HOUSES, item_building_prl5_office) {
    property {
        /* common properties */
        substitute:                 0x1C;
        name:                       string(STR_OFFICE);
//        override:                   0x1C;
        building_flags:             bitmask(HOUSE_FLAG_ANIMATE);
        population:                 120;
        mail_multiplier:            58;
        accepted_cargos:            [[PASS, 7], [MAIL, 2], [GOOD, 4]];
        local_authority_impact:     100;
        removal_cost_multiplier:    107;
        probability:                PLB_PROBABILITY;
        years_available:            [1970, 2000];
        minimum_lifetime:           LIFETIME_MAJOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_CENTRE), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        animation_info:             [ANIMATION_NON_LOOPING, 12];
        animation_speed:            4;
        watched_cargo_types:        [];
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_building_prl5_office_graphics;
//        cargo_type_accept:  switch_houses_cargo_type_accept;
        anim_control:       switch_anim_control_75;
    }
}
