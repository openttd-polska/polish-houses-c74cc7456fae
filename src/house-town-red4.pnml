// This is part of Polish Buildings Set NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Sprites and spritelayouts */
#define HOUSE_TOWN_RED4_SPRITES(Model, Xcoord) \
spriteset(sset_house_town_red4_##Model##_stages, "gfx/house-town-red4.png") { \
    tmpl_sprite_ground(Xcoord,  540, 1) \
    tmpl_sprite_build40(Xcoord,  440, 1) \
    tmpl_sprite_build40(Xcoord,  340, 1) \
} \
spriteset(sset_house_town_red4_##Model##_ground, "gfx/house-town-red4.png") { \
    tmpl_sprite_ground(Xcoord,  120, 1) \
} \
spriteset(sset_house_town_red4_##Model, "gfx/house-town-red4.png") { \
    tmpl_sprite_build40(Xcoord,   20, 1) \
} \
spriteset(sset_house_town_red4_##Model##_ground_snow, "gfx/house-town-red4.png") { \
    tmpl_sprite_ground(Xcoord,  280, 1) \
} \
spriteset(sset_house_town_red4_##Model##_snow, "gfx/house-town-red4.png") { \
    tmpl_sprite_build40(Xcoord,  180, 1) \
} \
spritelayout slay_house_town_red4_##Model##_stages { \
    ground { \
        sprite: GROUNDSPRITE_CLEARED; \
 	} \
    building { \
        sprite: sset_house_town_red4_##Model##_stages(LOAD_TEMP(2)); \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 40; \
        recolour_mode: RECOLOUR_NONE; \
    } \
} \
spritelayout slay_house_town_red4_##Model { \
    ground { \
        sprite: GROUNDSPRITE_NORMAL; \
 	} \
    childsprite { \
	    sprite: sset_house_town_red4_##Model##_ground; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_town_red4_##Model; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 40; \
        recolour_mode: RECOLOUR_NONE; \
    } \
} \
spritelayout slay_house_town_red4_##Model##_snow { \
    ground { \
        sprite: GROUNDSPRITE_SNOW; \
 	} \
    childsprite { \
	    sprite: sset_house_town_red4_##Model##_ground_snow; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_town_red4_##Model##_snow; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 40; \
        recolour_mode: RECOLOUR_NONE; \
    } \
}
/* Model #1 */
HOUSE_TOWN_RED4_SPRITES(1, 4)
/* Model #2 */
HOUSE_TOWN_RED4_SPRITES(2, 80)
/* Model #3 */
HOUSE_TOWN_RED4_SPRITES(3, 156)
/* Model #4 */
HOUSE_TOWN_RED4_SPRITES(4, 232)
/* Model #5 */
HOUSE_TOWN_RED4_SPRITES(5, 308)
/* Model #6 */
HOUSE_TOWN_RED4_SPRITES(6, 384)

/* Switches */
#define HOUSE_TOWN_RED4_SWITCH(Model) \
switch (FEAT_HOUSES, SELF, switch_house_town_red4_##Model##_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_town_red4_##Model##_stages; \
    return slay_house_town_red4_##Model; \
} \
switch (FEAT_HOUSES, SELF, switch_house_town_red4_##Model##_snow_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_town_red4_##Model##_stages; \
    return slay_house_town_red4_##Model##_snow; \
}
/* Model #1 */
HOUSE_TOWN_RED4_SWITCH(1)
/* Model #2 */
HOUSE_TOWN_RED4_SWITCH(2)
/* Model #3 */
HOUSE_TOWN_RED4_SWITCH(3)
/* Model #4 */
HOUSE_TOWN_RED4_SWITCH(4)
/* Model #5 */
HOUSE_TOWN_RED4_SWITCH(5)
/* Model #6 */
HOUSE_TOWN_RED4_SWITCH(6)

random_switch (FEAT_HOUSES, SELF, switch_house_town_familok_normal_graphics) {
    1: return switch_house_town_red4_1_graphics;
    1: return switch_house_town_red4_2_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_town_familok_snow_graphics) {
    1: return switch_house_town_red4_1_snow_graphics;
    1: return switch_house_town_red4_2_snow_graphics;
}
switch (FEAT_HOUSES, SELF, switch_house_town_familok_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_town_familok_snow_graphics;
    switch_house_town_familok_normal_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_town_tenement_se_normal_graphics) {
    1: return switch_house_town_red4_3_graphics;
    1: return switch_house_town_red4_4_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_town_tenement_se_snow_graphics) {
    1: return switch_house_town_red4_3_snow_graphics;
    1: return switch_house_town_red4_4_snow_graphics;
}
switch (FEAT_HOUSES, SELF, switch_house_town_tenement_se_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_town_tenement_se_snow_graphics;
    switch_house_town_tenement_se_normal_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_town_tenement_sw_normal_graphics) {
    1: return switch_house_town_red4_5_graphics;
    1: return switch_house_town_red4_6_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_town_tenement_sw_snow_graphics) {
    1: return switch_house_town_red4_5_snow_graphics;
    1: return switch_house_town_red4_6_snow_graphics;
}
switch (FEAT_HOUSES, SELF, switch_house_town_tenement_sw_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_town_tenement_sw_snow_graphics;
    switch_house_town_tenement_sw_normal_graphics;
}

/* House definition */
item (FEAT_HOUSES, item_house_town_familok) {
    property {
        /* common properties */
        substitute:                 0x1B;
        name:                       string(STR_HOUSE_FAMILOK);
        override:                   0x1B;
        building_flags:             bitmask();
        population:                 52;
        mail_multiplier:            32;
        accepted_cargos:            [[PASS, 5], [MAIL, 2], [GOOD, 3]];
        local_authority_impact:     100;
        removal_cost_multiplier:    105;
        probability:                PLB_PROBABILITY;
        years_available:            [PLB_START_DATE, 1955];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTER_SUBURB, TOWNZONE_OUTSKIRT), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_house_town_familok_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}

item (FEAT_HOUSES, item_house_town_tenement_se) {
    property {
        /* common properties */
        substitute:                 0x1B;
        name:                       string(STR_HOUSE_TENEMENT);
//        override:                   0x1B;
        building_flags:             bitmask();
        population:                 72;
        mail_multiplier:            42;
        accepted_cargos:            [[PASS, 6], [MAIL, 2], [GOOD, 3]];
        local_authority_impact:     100;
        removal_cost_multiplier:    105;
        probability:                PLB_PROBABILITY * 3;
        years_available:            [PLB_START_DATE, 1965];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTER_SUBURB, TOWNZONE_OUTSKIRT), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_house_town_tenement_se_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
        construction_check: return nearby_tile_class(0, 1) == TILE_CLASS_ROAD;
    }
}

item (FEAT_HOUSES, item_house_town_tenement_sw) {
    property {
        /* common properties */
        substitute:                 0x1B;
        name:                       string(STR_HOUSE_TENEMENT);
//        override:                   0x1B;
        building_flags:             bitmask();
        population:                 72;
        mail_multiplier:            42;
        accepted_cargos:            [[PASS, 6], [MAIL, 2], [GOOD, 3]];
        local_authority_impact:     100;
        removal_cost_multiplier:    105;
        probability:                PLB_PROBABILITY * 3;
        years_available:            [PLB_START_DATE, 1965];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTER_SUBURB, TOWNZONE_OUTSKIRT), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_house_town_tenement_sw_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
        construction_check: return nearby_tile_class(1, 0) == TILE_CLASS_ROAD;
    }
}