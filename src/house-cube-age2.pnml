// This is part of Polish Buildings Set NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Sprites and spritelayouts */
#define HOUSE_CUBE_AGE2_SPRITES(Model, Xcoord, Flags) \
spriteset(sset_house_cube_age2_##Model##_stages, "gfx/house-cube-age2.png") { \
    tmpl_sprite_ground(Xcoord,  540, 1) \
    tmpl_sprite_build40(Xcoord,  440, 1) \
    tmpl_sprite_build40(Xcoord,  340, 1) \
} \
spriteset(sset_house_cube_age2_##Model##_ground, "gfx/house-cube-age2.png") { \
    tmpl_sprite_ground(Xcoord,  120, 1) \
} \
spriteset(sset_house_cube_age2_##Model, "gfx/house-cube-age2.png") { \
    tmpl_sprite_build40(Xcoord,   20, 1) \
} \
spriteset(sset_house_cube_age2_##Model##_ground_snow, "gfx/house-cube-age2.png") { \
    tmpl_sprite_ground(Xcoord,  280, 1) \
} \
spriteset(sset_house_cube_age2_##Model##_snow, "gfx/house-cube-age2.png") { \
    tmpl_sprite_build40_flags(Xcoord,  180, 1, Flags) \
} \
spritelayout slay_house_cube_age2_##Model##_stages { \
    ground { \
        sprite: GROUNDSPRITE_CLEARED; \
 	} \
    building { \
        sprite: sset_house_cube_age2_##Model##_stages(LOAD_TEMP(2)); \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 16; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
} \
spritelayout slay_house_cube_age2_##Model { \
    ground { \
        sprite: GROUNDSPRITE_NORMAL; \
 	} \
    childsprite { \
	    sprite: sset_house_cube_age2_##Model##_ground; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_cube_age2_##Model; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 16; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
} \
spritelayout slay_house_cube_age2_##Model##_snow { \
    ground { \
        sprite: GROUNDSPRITE_SNOW; \
 	} \
    childsprite { \
	    sprite: sset_house_cube_age2_##Model##_ground_snow; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_cube_age2_##Model##_snow; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 16; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
}

/* Model #1 */
HOUSE_CUBE_AGE2_SPRITES(1, 4, 0)
/* Model #2 */
HOUSE_CUBE_AGE2_SPRITES(2, 80, 0)
/* Model #3 */
HOUSE_CUBE_AGE2_SPRITES(3, 156, 0)
/* Model #4 */
HOUSE_CUBE_AGE2_SPRITES(4, 232, ANIM)
/* Model #5 */
HOUSE_CUBE_AGE2_SPRITES(5, 308, 0)
/* Model #6 */
HOUSE_CUBE_AGE2_SPRITES(6, 384, 0)

/* Switches */
#define HOUSE_CUBE_AGE2_SWITCH(Model) \
switch (FEAT_HOUSES, SELF, switch_house_cube_age2_##Model##_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_cube_age2_##Model##_stages; \
    return slay_house_cube_age2_##Model; \
} \
switch (FEAT_HOUSES, SELF, switch_house_cube_age2_##Model##_snow_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_cube_age2_##Model##_stages; \
    return slay_house_cube_age2_##Model##_snow; \
}
/* Model #1 */
HOUSE_CUBE_AGE2_SWITCH(1)
/* Model #2 */
HOUSE_CUBE_AGE2_SWITCH(2)
/* Model #3 */
HOUSE_CUBE_AGE2_SWITCH(3)
/* Model #4 */
HOUSE_CUBE_AGE2_SWITCH(4)
/* Model #5 */
HOUSE_CUBE_AGE2_SWITCH(5)
/* Model #6 */
HOUSE_CUBE_AGE2_SWITCH(6)

random_switch (FEAT_HOUSES, SELF, switch_house_cube_age2_model_graphics) {
    6: return switch_house_cube_age2_1_graphics;
    5: return switch_house_cube_age2_2_graphics;
    5: return switch_house_cube_age2_3_graphics;
    6: return switch_house_cube_age2_4_graphics;
    5: return switch_house_cube_age2_5_graphics;
    5: return switch_house_cube_age2_6_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_cube_age2_model_snow_graphics) {
    6: return switch_house_cube_age2_1_snow_graphics;
    5: return switch_house_cube_age2_2_snow_graphics;
    5: return switch_house_cube_age2_3_snow_graphics;
    6: return switch_house_cube_age2_4_snow_graphics;
    5: return switch_house_cube_age2_5_snow_graphics;
    5: return switch_house_cube_age2_6_snow_graphics;
}
switch (FEAT_HOUSES, SELF, switch_house_cube_age2_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_cube_age2_model_snow_graphics;
    switch_house_cube_age2_model_graphics;
}

/* House definition */
item (FEAT_HOUSES, item_house_cube_age2) {
    property {
        /* common properties */
        substitute:                 0x25;
        name:                       string(STR_CUBE_HOUSE);
//        override:                   0x25;
        building_flags:             bitmask();
        population:                 15;
        mail_multiplier:            6;
        accepted_cargos:            [[PASS, 3], [MAIL, 1], [GOOD, 2]];
        local_authority_impact:     74;
        removal_cost_multiplier:    71;
        probability:                PLB_PROBABILITY * 8;
        years_available:            [1955, 1995];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTSKIRT, TOWNZONE_EDGE), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_house_cube_age2_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}
