// This is part of Polish Buildings Set NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

/* Sprites and spritelayouts */
#define HOUSE_AGE1_SPRITES(Model, Xcoord) \
spriteset(sset_house_age1_##Model##_stages, "gfx/houses-age1.png") { \
    tmpl_sprite_ground(Xcoord,  540, 1) \
    tmpl_sprite_build40(Xcoord,  440, 1) \
    tmpl_sprite_build40(Xcoord,  340, 1) \
} \
spriteset(sset_house_age1_##Model##_ground, "gfx/houses-age1.png") { \
    tmpl_sprite_ground(Xcoord,  120, 1) \
} \
spriteset(sset_house_age1_##Model, "gfx/houses-age1.png") { \
    tmpl_sprite_build40(Xcoord,   20, 1) \
} \
spriteset(sset_house_age1_##Model##_ground_snow, "gfx/houses-age1.png") { \
    tmpl_sprite_ground(Xcoord,  280, 1) \
} \
spriteset(sset_house_age1_##Model##_snow, "gfx/houses-age1.png") { \
    tmpl_sprite_build40(Xcoord,  180, 1) \
} \
spritelayout slay_house_age1_##Model##_stages { \
    ground { \
        sprite: GROUNDSPRITE_CLEARED; \
 	} \
    building { \
        sprite: sset_house_age1_##Model##_stages(LOAD_TEMP(2)); \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 16; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
} \
spritelayout slay_house_age1_##Model { \
    ground { \
        sprite: GROUNDSPRITE_NORMAL; \
 	} \
    childsprite { \
	    sprite: sset_house_age1_##Model##_ground; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_age1_##Model; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 16; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
} \
spritelayout slay_house_age1_##Model##_snow { \
    ground { \
        sprite: GROUNDSPRITE_SNOW; \
 	} \
    childsprite { \
	    sprite: sset_house_age1_##Model##_ground_snow; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_age1_##Model##_snow; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 16; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
}

/* Model #1 */
HOUSE_AGE1_SPRITES(1, 4)
/* Model #2 */
HOUSE_AGE1_SPRITES(2, 80)
/* Model #3 */
HOUSE_AGE1_SPRITES(3, 156)
/* Model #4 */
HOUSE_AGE1_SPRITES(4, 232)
/* Model #5 */
HOUSE_AGE1_SPRITES(5, 308)
/* Model #6 */
HOUSE_AGE1_SPRITES(6, 384)

/* Switches */
#define HOUSE_AGE1_SWITCH(Model) \
switch (FEAT_HOUSES, SELF, switch_house_age1_##Model##_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_age1_##Model##_stages; \
    return slay_house_age1_##Model; \
} \
switch (FEAT_HOUSES, SELF, switch_house_age1_##Model##_snow_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_age1_##Model##_stages; \
    return slay_house_age1_##Model##_snow; \
}
/* Model #1 */
HOUSE_AGE1_SWITCH(1)
/* Model #2 */
HOUSE_AGE1_SWITCH(2)
/* Model #3 */
HOUSE_AGE1_SWITCH(3)
/* Model #4 */
HOUSE_AGE1_SWITCH(4)
/* Model #5 */
HOUSE_AGE1_SWITCH(5)
/* Model #6 */
HOUSE_AGE1_SWITCH(6)

switch (FEAT_HOUSES, SELF, switch_house_cottage_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_age1_1_snow_graphics;
    switch_house_age1_1_graphics;
}
switch (FEAT_HOUSES, SELF, switch_house_old_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_age1_2_snow_graphics;
    switch_house_age1_2_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_old_big_model_graphics) {
    5: return switch_house_age1_3_graphics;
    3: return switch_house_age1_4_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_old_big_model_snow_graphics) {
    5: return switch_house_age1_3_snow_graphics;
    3: return switch_house_age1_4_snow_graphics;
}
switch (FEAT_HOUSES, SELF, switch_house_old_big_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_old_big_model_snow_graphics;
    switch_house_old_big_model_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_farm_model_graphics) {
    5: return switch_house_age1_5_graphics;
    3: return switch_house_age1_6_graphics;
}
random_switch (FEAT_HOUSES, SELF, switch_house_farm_model_snow_graphics) {
    5: return switch_house_age1_5_snow_graphics;
    3: return switch_house_age1_6_snow_graphics;
}
switch (FEAT_HOUSES, SELF, switch_house_farm_graphics, terrain_type) {
    TILETYPE_SNOW: switch_house_farm_model_snow_graphics;
    switch_house_farm_model_graphics;
}

/* House definition */
item (FEAT_HOUSES, item_house_farm) {
    property {
        /* common properties */
        substitute:                 0x51;
        name:                       string(STR_FARM_HOUSE);
//        override:                   0x51;
        building_flags:             bitmask();
        population:                 7;
        mail_multiplier:            3;
        accepted_cargos:            [[PASS, 1], [MAIL, 1]];
        local_authority_impact:     55;
        removal_cost_multiplier:    55;
        probability:                PLB_PROBABILITY * 0.5;
        years_available:            [PLB_START_DATE, 1955];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_EDGE), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             0;
    }

    graphics {
        default:            switch_house_farm_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}

item (FEAT_HOUSES, item_house_cottage) {
    property {
        /* common properties */
        substitute:                 0x19;
        name:                       string(STR_COTTAGE_HOUSE);
//        override:                   0x19;
        building_flags:             bitmask();
        population:                 9;
        mail_multiplier:            4;
        accepted_cargos:            [[PASS, 1], [MAIL, 1]];
        local_authority_impact:     70;
        removal_cost_multiplier:    65;
        probability:                PLB_PROBABILITY;
        years_available:            [PLB_START_DATE, 1965];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_EDGE), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             0;
    }

    graphics {
        default:            switch_house_cottage_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}

item (FEAT_HOUSES, item_house_old) {
    property {
        /* common properties */
        substitute:                 0x18;
        name:                       string(STR_SMALL_HOUSE);
//        override:                   0x18;
        building_flags:             bitmask();
        population:                 11;
        mail_multiplier:            5;
        accepted_cargos:            [[PASS, 2], [MAIL, 1], [GOOD, 1]];
        local_authority_impact:     72;
        removal_cost_multiplier:    67;
        probability:                PLB_PROBABILITY;
        years_available:            [PLB_START_DATE, 1965];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTSKIRT, TOWNZONE_EDGE), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             0;
    }

    graphics {
        default:            switch_house_old_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}

item (FEAT_HOUSES, item_house_old_big) {
    property {
        /* common properties */
        substitute:                 0x1A;
        name:                       string(STR_MEDIUM_HOUSE);
//        override:                   0x1A;
        building_flags:             bitmask();
        population:                 13;
        mail_multiplier:            5;
        accepted_cargos:            [[PASS, 3], [MAIL, 1], [GOOD, 1]];
        local_authority_impact:     74;
        removal_cost_multiplier:    71;
        probability:                PLB_PROBABILITY * 2;
        years_available:            [PLB_START_DATE, 1965];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTSKIRT, TOWNZONE_EDGE), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE, COLOUR_DARK_BLUE];
        refresh_multiplier:         0;
        // no animation info
        building_class:             0;
    }

    graphics {
        default:            switch_house_old_big_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}
