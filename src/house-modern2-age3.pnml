// This is part of Polish Buildings Set NewGRF.
// Don't include any headers inside this file!
// (c) Tadeusz Domagalski, licence: GPLv2

#define HOUSE_MODERN2_AGE3_FILE "gfx/house-modern2-age3.png"

/* Sprites and spritelayouts */
#define HOUSE_MODERN2_AGE3_SPRITES(Model, Xcoord, Flags) \
spriteset(sset_house_modern2_age3_##Model##_stages, HOUSE_MODERN2_AGE3_FILE) { \
    tmpl_sprite_ground_flags(Xcoord, 380, 1, ANIM) \
    tmpl_sprite_build40_flags(Xcoord, 280, 1, ANIM) \
    tmpl_sprite_build40_flags(Xcoord, 180, 1, ANIM) \
} \
spriteset(sset_house_modern2_age3_##Model##_ground, HOUSE_MODERN2_AGE3_FILE) { \
    tmpl_sprite_ground(Xcoord, 120, 1) \
} \
spriteset(sset_house_modern2_age3_##Model, HOUSE_MODERN2_AGE3_FILE) { \
    tmpl_sprite_build40_flags(Xcoord, 20, 1, Flags) \
} \
spriteset(sset_house_modern2_age3_##Model##_ground_snow, HOUSE_MODERN2_AGE3_FILE) { \
    tmpl_sprite_ground(Xcoord+76, 120, 1) \
} \
spriteset(sset_house_modern2_age3_##Model##_snow, HOUSE_MODERN2_AGE3_FILE) { \
    tmpl_sprite_build40_flags(Xcoord+76, 20, 1, Flags) \
} \
spritelayout slay_house_modern2_age3_##Model##_stages { \
    ground { \
        sprite: GROUNDSPRITE_CLEARED; \
 	} \
    building { \
        sprite: sset_house_modern2_age3_##Model##_stages(LOAD_TEMP(2)); \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 24; \
        recolour_mode: RECOLOUR_NONE; \
    } \
} \
spritelayout slay_house_modern2_age3_##Model { \
    ground { \
        sprite: GROUNDSPRITE_NORMAL; \
 	} \
    childsprite { \
	    sprite: sset_house_modern2_age3_##Model##_ground; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_modern2_age3_##Model; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 24; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
} \
spritelayout slay_house_modern2_age3_##Model##_snow { \
    ground { \
        sprite: GROUNDSPRITE_SNOW; \
 	} \
    childsprite { \
	    sprite: sset_house_modern2_age3_##Model##_ground_snow; \
		always_draw: 1; \
	} \
    building { \
        sprite: sset_house_modern2_age3_##Model##_snow; \
        xoffset: 0; \
        yoffset: 0; \
        zoffset: 0; \
        xextent: 16; \
        yextent: 16; \
        zextent: 24; \
        recolour_mode: RECOLOUR_REMAP; \
        palette: PALETTE_USE_DEFAULT; \
    } \
}

/* Model #1 */
HOUSE_MODERN2_AGE3_SPRITES(1, 4, NOANIM)
/* Model #2 */
HOUSE_MODERN2_AGE3_SPRITES(2, 156, NOANIM)
/* Model #3 */
HOUSE_MODERN2_AGE3_SPRITES(3, 308, ANIM)

/* Switches */
#define HOUSE_MODERN2_AGE3_SWITCH(Model) \
switch (FEAT_HOUSES, SELF, switch_house_modern2_age3_##Model##_normal_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_modern2_age3_##Model##_stages; \
    return slay_house_modern2_age3_##Model; \
} \
switch (FEAT_HOUSES, SELF, switch_house_modern2_age3_##Model##_snow_graphics, [ \
    STORE_TEMP(construction_state, 2), construction_state]) { \
    0..2: return slay_house_modern2_age3_##Model##_stages; \
    return slay_house_modern2_age3_##Model##_snow; \
} \
switch (FEAT_HOUSES, SELF, switch_house_modern2_age3_##Model##_climate_graphics, terrain_type) { \
    TILETYPE_SNOW: switch_house_modern2_age3_##Model##_snow_graphics; \
    switch_house_modern2_age3_##Model##_normal_graphics; \
}

/* Model #1 */
HOUSE_MODERN2_AGE3_SWITCH(1)
/* Model #2 */
HOUSE_MODERN2_AGE3_SWITCH(2)
/* Model #3 */
HOUSE_MODERN2_AGE3_SWITCH(3)

random_switch (FEAT_HOUSES, SELF, switch_house_modern2_age3_curved_graphics) {
    4: switch_house_modern2_age3_1_climate_graphics;
    4: switch_house_modern2_age3_2_climate_graphics;
}

/* House definition */
item (FEAT_HOUSES, item_house_modern2_age3_curved) {
    property {
        /* common properties */
        substitute:                 0x02;
        name:                       string(STR_FLATS_SMALL);
        override:                   0x02;
        building_flags:             bitmask();
        population:                 35;
        mail_multiplier:            6;
        accepted_cargos:            [[PASS, 4], [MAIL, 1], [GOOD, 1]];
        local_authority_impact:     74;
        removal_cost_multiplier:    71;
        probability:                PLB_PROBABILITY * 2;
        years_available:            [1993, 9999];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTER_SUBURB, TOWNZONE_OUTSKIRT), PLB_CLIMATES];
        random_colours:             [COLOUR_BLUE, COLOUR_RED, COLOUR_PURPLE, COLOUR_YELLOW];
        refresh_multiplier:         0;
        // no animation info
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_house_modern2_age3_curved_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}

item (FEAT_HOUSES, item_house_modern2_age3_simple) {
    property {
        /* common properties */
        substitute:                 0x02;
        name:                       string(STR_FLATS_SMALL);
//        override:                   0x02;
        building_flags:             bitmask();
        population:                 30;
        mail_multiplier:            5;
        accepted_cargos:            [[PASS, 3], [MAIL, 1], [GOOD, 1]];
        local_authority_impact:     74;
        removal_cost_multiplier:    71;
        probability:                PLB_PROBABILITY;
        years_available:            [1989, 9999];
        minimum_lifetime:           LIFETIME_MINOR_HOUSE;
        availability_mask:          [bitmask(TOWNZONE_OUTER_SUBURB, TOWNZONE_OUTSKIRT), PLB_CLIMATES];
        random_colours:             [COLOUR_DARK_GREEN, COLOUR_CREAM, COLOUR_LIGHT_BLUE, COLOUR_PINK];
        refresh_multiplier:         0;
        // no animation info
        building_class:             CLASS_NONE;
    }

    graphics {
        default:            switch_house_modern2_age3_3_climate_graphics;
        cargo_type_accept:  switch_houses_cargo_type_accept;
    }
}
